// https://github.com/inkle/ink/blob/master/Documentation/WritingWithInk.md

start: (NEWLINE | stmt)+

?stmt: text
      | options
      | knot
      | divert
      | TODO              -> todo

// Multiple line of plain text with tags
text: (GLUESTART? STRING GLUEEND? divert? tag? NEWLINE*)+
// text doesn't starts with:
// #: comment
// *: option
// =: knot
// //: comment
// /*: comment
// ->: divert
// <>: glue
TEXTSTART: /[^#\n\*=\-\/ \t<]/
         | "-" /[^>]/
         | "/" /[^\/\*]/
         | "<" /[^>]/
TEXTBODY: /
    ([^#\n\-<]
     |-[^>][^#\n\-]* # - but not divert
     |<[^>][^#\n\<]* # < but not glue
    )*
/x

STRING: WS_INLINE* TEXTSTART TEXTBODY
GLUE: "<>"
GLUESTART: GLUE
GLUEEND: GLUE

// Option parsing
// * opt1
//   opt1 content text
//   -> divert in opt1
// * opt2 [suppress] with text -> divert # with tag support
options: option+
opttext: OPTSTRING? ("[" OPTSUPPRESS? "]" OPTPOST?)? divert? tag?
option: WS_INLINE* "*" opttext NEWLINE (text | ndivert NEWLINE?)*
OPTSTRING: /[^#\n\[\]\-]+/
OPTSUPPRESS: OPTSTRING
OPTPOST: OPTSTRING

// Knot
// === knot_name ===
// knot content that can be
// * text
// * options
// * divert
knotheader: "===" /[^\n=]+/ "==="?
knot: knotheader NEWLINE (NEWLINE | text | options | ndivert)

// Divert
// -> knot_name
ndivert: WS_INLINE* divert
divert: "->" /[^\n]+/

TODO: "TODO:" STRING
?tag: SH_COMMENT

%import common.NEWLINE
%import common.CPP_COMMENT
%import common.C_COMMENT
%import common.SH_COMMENT
%import common.WS_INLINE
%import common.CNAME

%ignore CPP_COMMENT
%ignore C_COMMENT
%ignore TODO
