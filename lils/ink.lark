// https://github.com/inkle/ink/blob/master/Documentation/WritingWithInk.md

start: (NEWLINE | stmt)+

?stmt: text
     | include
     | options
     | knot
     | divert
     | TODO              -> todo

// Multiple line of plain text with tags
text: (GLUESTART? STRING GLUEEND? divert? tag? NEWLINE*)+
// text doesn't starts with:
// #: comment
// *: option
// =: knot
// //: comment
// /*: comment
// ->: divert
// <>: glue
TEXTSTART.1: /[^#\*=\-\/\t<\n ]/
           | "-" /[^>]/
           | "/" /[^\/\*]/
           | "<" /[^>]/
TEXTBODY: /
    ([^#\n\-<]
     |-[^>][^#\n\-]* # - but not divert
     |<[^>][^#\n\<]* # < but not glue
    )*
/x

STRING.0: WS_INLINE* (TEXTSTART TEXTBODY | NEWLINE)
GLUE: "<>"
GLUESTART: GLUE
GLUEEND: GLUE

// Option parsing
// * opt1
//   opt1 content text
//   -> divert in opt1
// * opt2 [suppress] with text -> divert # with tag support
options: option+
opttext: OPTSTRING? ("[" OPTSUPPRESS? "]" OPTPOST?)? divert? tag?
option: WS_INLINE* OPT opttext NEWLINE (text | ndivert NEWLINE?)*
OPTSTRING: /[^#\n\[\]\-=]+/
OPTSUPPRESS: OPTSTRING
OPTPOST: OPTSTRING

// Knot
// === knot_name ===
// knot content that can be
// * text
// * options
// * divert
knotheader: KNOT WS_INLINE* CNAME WS_INLINE* "==="?
knot: knotheader NEWLINE content? stitch*

// Stitches
// = stitch_name
// stitch content can be
stitchheader: STITCH WS_INLINE* CNAME
stitch: stitchheader NEWLINE content

content: (text | options | ndivert | NEWLINE)+

// Divert
// -> knot_name
// -> knot_name.stitch
ndivert: WS_INLINE* divert
divert: "->" WS_INLINE* divert_name
divert_name: CNAME divert_stitch?
divert_stitch: DOT CNAME
// Give priority to avoid string matching instead of divert in options
DOT.2: "."

include: INCLUDE WS_INLINE* FILENAME WS_INLINE* NEWLINE
INCLUDE.5: "INCLUDE"
FILENAME.1: /[^\n ]/+ ".ink"

TODO: "TODO:" STRING
?tag: SH_COMMENT

// The priority in this rules is important, KNOT has higher priority thant
// STITCH
KNOT.3: WS_INLINE* "==="
STITCH.2: WS_INLINE* "="
OPT.1: "*"

NEWLINE.1: CR? LF

%import common.CR
%import common.LF
%import common.CPP_COMMENT
%import common.C_COMMENT
%import common.SH_COMMENT
%import common.WS_INLINE
%import common.CNAME

%ignore CPP_COMMENT
%ignore C_COMMENT
%ignore TODO
